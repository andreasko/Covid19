#+SETUPFILE: ~/org-blog/setup.org

#+PROPERTY: header-args: :eval never-export
#+PROPERTY: header-args:julia :eval never-export :exports both :kernel julia-4-threads-1.4 :session /ssh:grothendieck:/homes/tef30/.local/share/jupyter/runtime/kernel-3f2d796a-6f9b-4194-abf9-00f9ae2d9247.json :tangle ../scripts/03-visualization.jl

#+HTML_HEAD: <link rel="stylesheet" type="text/css" href="/home/tor/org-blog/assets/css/main.css"/>
#+HTML_HEAD: <style>body { width: 60%; margin: 0 auto; } div.figure > p > img { max-height: 700px; }  </style>
#+HTML_HEAD: <style>div.two-by-two { height: 100%;display: flex;flex-direction: row;flex-wrap: wrap; } div.two-by-two > div.figure { width: 45%; margin: 0 auto; } </style>

#+TITLE: Replication study: Estimating number of inections and impact of NPIs on COVID-19 in European countries (Imperial Report 13)

#+begin_src emacs-lisp :results none :exports none :eval never-export
(setq org-preview-latex-image-directory "/home/tor/.ltximg/")
#+end_src

#+begin_src emacs-lisp :results none :exports none :eval never-export
;; necessary to ensure that remote works without issues
(add-to-list 'tramp-remote-path 'tramp-own-remote-path)
#+end_src

#+begin_src julia :exports none :results none :tangle no
# just to make sure we're in the correct directory
_home = ENV["HOME"]
cd("$(_home)/Projects/mine/Covid19/")
#+end_src

#+begin_src julia :exports none :tangle no
pwd()
#+end_src

* Introduction
We, i.e. the [[https://turing.ml/dev/team/][TuringLang team]], are currently exploring cooperation with other researchers in attempt to help with the ongoing crisis. As preparation for this and to get our feet wet, we decided it would be useful to do a replication study of the [[https://www.imperial.ac.uk/mrc-global-infectious-disease-analysis/covid-19/report-13-europe-npi-impact/][Imperial Report 13]]. We figured it might be useful for the public, in particular other researchers working on the same or similar models, to see the results of this analysis, and thus decided to make it available here.

We want to emphasize that you should look to the original paper rather than this post for developments and analysis of the model. As of right now, we're not going to be making any claims about the validity of the model nor about what the results imply. This post's purpose is only to add tiny bit of validation to the /inference/ performed in the paper by obtaining the same results using a different probabilistic programming language (PPL) and to explore whether or not =Turing.jl= can be useful for researchers working on these problems.

The full code for the =Covid19.jl= package you can find [[https://github.com/TuringLang/Covid19][here]].

* Setup
This is all assuming that you're in the project directory of [[https://github.com/TuringLang/Covid19][=Covid19.jl=]], a small package where we gather most of our ongoing work.

In the project we use [[https://github.com/JuliaDynamics/DrWatson.jl][=DrWatson.jl=]] which provides a lot of convenient functionality for, well, working with a project. The below code will activate the =Covid19.jl= project to ensure that we're using correct versions of all the dependencies, i.e. code is reproducible. It's basically just doing =] activate= but with a few bells and whistles that we'll use later.

#+begin_src julia :results none :exports code
using DrWatson
quickactivate(@__DIR__)
#+end_src

#+begin_src julia :exports none
using Pkg
Pkg.instantiate()
#+end_src

With the project activate, we can import the =Covid19.jl= package:

#+begin_src julia
# Loading the project (https://github.com/TuringLang/Covid19)
using Covid19
#+end_src

#+begin_src julia
# Some other packages we'll need
using Random, Dates, Turing, Bijectors
#+end_src

And we'll be using the new [[https://julialang.org/blog/2019/07/multithreading/][multithreading functionality in Julia]] to speed things up, so we need the =Base.Threads= package.

#+begin_src julia
using Base.Threads
nthreads()
#+end_src

#+begin_src julia :exports none
begin
    dist = product_distribution(fill(truncated(Normal(3.28, 10), 0, Inf), 14))
    b = bijector(dist)
    x = rand(dist)

    # stupid check to make sure that we have the correct versions of Bijectors.jl used WITHIN Turing.jl
    @assert (invlink(dist, b(x)) ≈ Turing.invlink(dist, b(x))) && (Turing.invlink(dist, b(x)) ≈ x)
end
#+end_src

#+begin_src julia :exports none
begin
    @assert length(unique(rand(Turing.filldist(Gamma(.1667, 1), 6)))) > 1
end
#+end_src

Here's a summary of the setup:

#+begin_src julia :exports both
using Pkg
Pkg.status()
#+end_src
In the Github project you will find a =Manifest.toml=. This means that if you're working directory is the project directory, and you do =julia --project= followed by =] instantiate= you will have /exactly/ the same enviroment as we had when performing this analysis.

* Data
As mentioned =DrWatson.jl= provides a lot of convenience functions for working in a project, and one of them is =projectdir(args...)= which will resolve join the =args= to the absolute path of the project directory. In similar fashion it provides a =datadir= method which defaults to =projectdir("data")=. But to remove the possibility of making a type later on when writing =datadir("imperial-report13")=, we'll overload this method for this notebook:

#+begin_src julia
import DrWatson: datadir

datadir() = projectdir("data", "imperial-report13")
datadir(s...) = projectdir("data", "imperial-report13", s...)
#+end_src

To ensure consistency with the original model from the paper (and to stay up-to-date with the changes made), we preprocessed the data using the =base.r= script from the [[https://github.com/ImperialCollegeLondon/covid19model/tree/6ee3010a58a57cc14a16545ae897ca668b7c9096][original repository (#6ee3010)]] and store the processed data in a =processed.rds= file. To load this RDS file obtained from the R script, we make use of the RData.jl package which allows us to load the RDS file into a Julia =Dict=.

#+begin_src julia
using RData
#+end_src

#+begin_src julia :exports code
rdata_full = load(datadir("processed_new.rds"))
rdata = rdata_full["stan_data"];
#+end_src

#+begin_src julia
keys(rdata_full)
#+end_src

#+begin_src julia
country_to_dates = d = Dict([(k, rdata_full["dates"][k]) for k in keys(rdata_full["dates"])])
#+end_src

Since the data-format is not native Julia there might be some discrepancies in the /types/ of the some data fields, and so we need to do some type-conversion of the loaded =rdata=. We also rename a lot of the fields to make it more understandable and for an easier mapping to our implementation of the model. Feel free to skip this snippet.

#+HTML: <details><summary>Data wrangling</summary>

#+begin_src julia
# Convert some misparsed fields
rdata["N2"] = Int(rdata["N2"]);
rdata["N0"] = Int(rdata["N0"]);

rdata["EpidemicStart"] = Int.(rdata["EpidemicStart"]);

rdata["cases"] = Int.(rdata["cases"]);
rdata["deaths"] = Int.(rdata["deaths"]);

# Stan will fail if these are `nothing` so we make them empty arrays
rdata["x"] = []
rdata["features"] = []

countries = (
  "Denmark",
  "Italy",
  "Germany",
  "Spain",
  "United_Kingdom",
  "France",
  "Norway",
  "Belgium",
  "Austria", 
  "Sweden",
  "Switzerland",
  "Greece",
  "Portugal",
  "Netherlands"
)
num_countries = length(countries)

names_covariates = ("schools_universities", "self_isolating_if_ill", "public_events", "any", "lockdown", "social_distancing_encouraged")
lockdown_index = findfirst(==("lockdown"), names_covariates)


function rename!(d, names::Pair...)
    # check that keys are not yet present before updating `d`
    for k_new in values.(names)
        @assert k_new ∉ keys(d) "$(k_new) already in dictionary"
    end
    
    for (k_old, k_new) in names
        d[k_new] = pop!(d, k_old)
    end
    return d
end

# `rdata` is a `DictOfVector` so we convert to a simple `Dict` for simplicity
d = Dict([(k, rdata[k]) for k in keys(rdata)]) # `values(df)` and `keys(df)` have different ordering so DON'T do `Dict(keys(df), values(df))`

# Rename some columns
rename!(
    d,
    "f" => "π", "SI" => "serial_intervals", "pop" => "population",
    "M" => "num_countries", "N0" => "num_impute", "N" => "num_obs_countries",
    "N2" => "num_total_days", "EpidemicStart" => "epidemic_start",
    "X" => "covariates", "P" => "num_covariates"
)

# Add some type-information to arrays and replace `-1` with `missing` (as `-1` is supposed to represent, well, missing data)
d["deaths"] = Int.(d["deaths"])
# d["deaths"] = replace(d["deaths"], -1 => missing)
d["deaths"] = collect(eachcol(d["deaths"])) # convert into Array of arrays instead of matrix

d["cases"] = Int.(d["cases"])
# d["cases"] = replace(d["cases"], -1 => missing)
d["cases"] = collect(eachcol(d["cases"])) # convert into Array of arrays instead of matrix

d["num_covariates"] = Int(d["num_covariates"])
d["num_countries"] = Int(d["num_countries"])
d["num_total_days"] = Int(d["num_total_days"])
d["num_impute"] = Int(d["num_impute"])
d["num_obs_countries"] = Int.(d["num_obs_countries"])
d["epidemic_start"] = Int.(d["epidemic_start"])
d["population"] = Int.(d["population"])

d["π"] = collect(eachcol(d["π"])) # convert into Array of arrays instead of matrix

# Convert 3D array into Array{Matrix}
covariates = [rdata["X"][m, :, :] for m = 1:num_countries]

data = (; (k => d[String(k)] for k in [:num_countries, :num_impute, :num_obs_countries, :num_total_days, :cases, :deaths, :π, :epidemic_start, :population, :serial_intervals])...)
data = merge(data, (covariates = covariates, ));
#+end_src

#+HTML: </details>

#+begin_src julia
data.num_countries
#+end_src

Because it's a bit much to visualize 14 countries at each step, we're going to use UK as an example throughout.

#+begin_src julia
uk_index = findfirst(==("United_Kingdom"), countries)
#+end_src

* Model
For a thorough description of the model and the assumptions that have gone into it, we recommend looking at the [[https://www.imperial.ac.uk/mrc-global-infectious-disease-analysis/covid-19/report-13-europe-npi-impact/][original paper]] or their very nice [[https://github.com/ImperialCollegeLondon/covid19model/tree/6ee3010a58a57cc14a16545ae897ca668b7c9096/Technical_description_of_Imperial_COVID_19_Model.pdf][techical report from the repository]]. The model described here is the one corresponding to the technical report linked. The link points to the correct commit ID and so should be consistent with this post despite potential changes to the "official" model made in the future.

For the sake of exposition, we present a compact version of the model here:
\begin{align}
  \tau & \sim \mathrm{Exponential}(1 / 0.03) \\
  y_m & \sim \mathrm{Exponential}(\tau) \quad & \text{for} \quad m = 1, \dots, M \\
  \kappa & \sim \mathcal{N}^{ + }(0, 0.5) \\
  \mu_m & \sim \mathcal{N}^{ + }(3.28, \kappa) \quad & \text{for} \quad m = 1, \dots, M \\
  \gamma & \sim \mathcal{N}^{ + }(0, 0.2) \\
  \beta_m & \sim \mathcal{N}(0, \gamma) \quad & \text{for} \quad m = 1, \dots, M \\
  \tilde{\alpha}_k &\sim \mathrm{Gamma}(0.1667, 1) \quad & \text{for} \quad k = 1, \dots, K \\
  \alpha_k &= \tilde{\alpha}_k - \frac{\log(1.05)}{6} \quad & \text{for} \quad  k = 1, \dots, K \\
  R_{t, m} &= \mu_m \exp(- \beta_m x_{k_{\text{ld}}} - \sum_{k=1}^{K} \alpha_k x_k) \quad & \text{for} \quad m = 1, \dots, M, \ t = 1, \dots, T  \\
  c_{t, m} &= R_{t, m} \bigg(1 - \frac{c_{t - 1, m}}{p_m} \bigg) \sum_{\tau = 1}^{t - 1} c_{\tau, m} s_{t - \tau} \quad & \text{for} \quad m = 1, \dots, M, \ t = 1, \dots, T \\
  \varepsilon_m^{\text{ifr}} &\sim \mathcal{N}(1, 0.1)^{ + } \quad & \text{for} \quad m = 1, \dots, M \\
  \mathrm{ifr}_m^{ * } &\sim \mathrm{ifr}_m \cdot \varepsilon_m^{\text{ifr}} \quad & \text{for} \quad m = 1, \dots, M \\
  d_{t, m} &= \mathrm{ifr}_m^{ * } \sum_{\tau=1}^{t - 1} c_{\tau, m} \pi_{t - \tau} \quad & \text{for} \quad m = 1, \dots, M, \ t = 1, \dots, T \\
  \phi  & \sim \mathcal{N}^{ + }(0, 5) \\
  D_{t, m} &\sim \mathrm{NegativeBinomial}(d_{t, m}, \phi) \quad & \text{for} \quad m = 1, \dots, M, \ t = 1, \dots, T 
\end{align}
where
- $\beta_m$ denotes the weight for the =lockdown= intervention (whose index we denote by $k_{\text{ld}}$)
  - Note that there is also a $\alpha_{k_{\text{ld}}}$ which is shared between all the $M$ countries
  - In contrast, the $\beta_m$ weight is local to the country with index $m$
  - This is a sort of  way to try and deal with the fact that =lockdown= means different things in different countries, e.g. =lockdown= in UK is much more severe than "lockdown" in Norway.
- $\mu_m$ represents the $R_0$ value for country $m$ (i.e. $R_t$ without any interventions)
- $\mathrm{ifr}_m$ denotes the /infection-fatality ratio/ for country $m$, and $\mathrm{ifr}_m^{ * }$ the /adjusted/ infection-fatality ratio (see paper)
- $\varepsilon_m^{\text{ifr}}$ denotes the noise for the multiplicative noise for the $\mathrm{ifr}_m^{ * }$
- $R_{t, m}$ denotes the /R-value/ at time $t$ for country $m$
- $p_{m}$ denotes the total/initial population for country $m$
- $\pi$ denotes the /time from infection to death/ and is assumed to be a sum of two independent random times: the incubation period (/infection-to-onset/) and time between onset of symptoms and death (/onset-to-death/):
  \begin{equation*}
  \pi \sim \mathrm{Gamma}(5.1, 0.86) + \mathrm{Gamma}(18.8, 0.45)
  \end{equation*}
  where in this case the $\mathrm{Gamma}$ is parameterized by its mean and coefficient of variation. In the model, this is /precomputed/ quantity and note something to that is to be inferred.
- $\pi_t$ then denotes a discretized version of the PDF for $\pi$. The reasoning behind the discretization is that if we assume $d_m(t)$ to be a continuous random variable denoting the death-rate at any time $t$, then it would be given by
  \begin{equation*}
  d_m(t) = \mathrm{ifr}_m^{ * } \int_0^t c_m(\tau) \pi(t - \tau) \dd{t}
  \end{equation*}
  i.e. the convolution of the number of cases observed at time time $\tau$, $c_m(\tau)$, and the /probability/ of death at prior to time $t$ for the new cases observed at time $\tau$, $\pi(t - \tau)$ (assuming stationarity of $\pi(t)$). Thus, $c_m(\tau) \pi(t - \tau)$ can be interpreted as the portion people who got the virus at time $\tau$ have died at time $t$ (or rather, have died after having the virus for $t - \tau$ time, with $t > \tau$). Discretizing then results in the above model.
- $s_t$ denotes the /serial intervals/, i.e. the time between successive cases in a chain of transmission
- $c_{t, m}$ denotes the /expected daily cases/ at time $t$ for country $m$
- $d_{t, m}$ denotes the /expected daily deaths/ at time $t$ for country $m$
- $D_{t, m}$ denotes the /daily deaths/ at time $t$ for country $m$ (in our case, this is the *likelihood*)

To see the reasoning for the choices of distributions and parameters for the priors, see the either the paper or the [[https://github.com/ImperialCollegeLondon/covid19model/tree/6ee3010a58a57cc14a16545ae897ca668b7c9096/Technical_description_of_Imperial_COVID_19_Model.pdf][techical report from the repository]].

** Code
In =Turing.jl=, a "sample"-statement is defined by =x ~ Distribution=. Therefore, if we convert the priors from the model into a block of code that will go into the =Turing.jl= =Model=, we get:

#+name: jl:priors
#+begin_src julia :exports code :eval no :tangle no
τ ~ Exponential(1 / 0.03) # `Exponential` has inverse parameterization of the one in Stan
y ~ filldist(Exponential(τ), num_countries)
ϕ ~ truncated(Normal(0, 5), 0, Inf)
κ ~ truncated(Normal(0, 0.5), 0, Inf)
μ ~ filldist(truncated(Normal(3.28, κ), 0, Inf), num_countries)

α_hier ~ filldist(Gamma(.1667, 1), num_covariates)
α = α_hier .- log(1.05) / 6.

ifr_noise ~ filldist(truncated(Normal(1., 0.1), 0, Inf), num_countries)

# lockdown-related
γ ~ truncated(Normal(0, 0.2), 0, Inf)
lockdown ~ filldist(Normal(0, γ), num_countries)
#+end_src

The only anamoly in the above snippet is the use of =filldist= which is just a way to construct an efficient =MultivariateDistribution= from which we can draw i.i.d. samples from a =UnivariateDistribution= in a vectorized manner.

And the full model is defined:

#+begin_src julia :noweb yes :tangle no
@model model_v2(
    num_impute,        # [Int] num. of days for which to impute infections
    num_total_days,    # [Int] days of observed data + num. of days to forecast
    cases,             # [AbstractVector{<:AbstractVector{<:Int}}] reported cases
    deaths,            # [AbstractVector{<:AbstractVector{<:Int}}] reported deaths; rows indexed by i > N contain -1 and should be ignored
    π,                 # [AbstractVector{<:AbstractVector{<:Real}}] h * s
    covariates,        # [Vector{<:AbstractMatrix}]
    epidemic_start,    # [AbstractVector{<:Int}]
    population,        # [AbstractVector{<:Real}]
    serial_intervals,  # [AbstractVector{<:Real}] fixed pre-calculated serial interval (SI) using empirical data from Neil
    lockdown_index,    # [Int] the index for the `lockdown` covariate in `covariates`
    predict=false,     # [Bool] if `false`, will only compute what's needed to `observe` but not more
    ::Type{TV} = Vector{Float64}
) where {TV} = begin
    # `covariates` should be of length `num_countries` and each entry correspond to a matrix of size `(num_total_days, num_covariates)`
    num_covariates = size(covariates[1], 2)
    num_countries = length(cases)
    num_obs_countries = Vector(undef, num_countries)
    for m = 1:num_countries
        num_obs_countries[m] = length(cases[m])
    end

    # Latent variables
    <<jl:priors>>
    
    # If we don't want to predict the future, we only need to compute up-to time-step `num_obs_countries[m]`
    last_time_steps = predict ? fill(num_total_days, num_countries) : num_obs_countries

    # Initialization of some quantities
    daily_cases_pred = TV[TV(undef, last_time_steps[m]) for m in 1:num_countries]
    cases_pred = TV[TV(undef, last_time_steps[m]) for m in 1:num_countries]
    expected_daily_deaths = TV[TV(undef, last_time_steps[m]) for m in 1:num_countries]
    Rₜ = TV[TV(undef, last_time_steps[m]) for m in 1:num_countries]
    Rₜ_adj = TV[TV(undef, last_time_steps[m]) for m in 1:num_countries]

    # Loops over countries and perform independent computations for each country
    # since this model does not include any notion of migration across borders.
    # => might has well wrap it in a `@threads` to perform the computation in parallel.
    @threads for m = 1:num_countries
        # country-specific parameters
        πₘ = π[m]
        pop_m = population[m]
        daily_cases_pred_m = daily_cases_pred[m]
        cases_pred_m = cases_pred[m]
        expected_daily_deaths_m = expected_daily_deaths[m]
        Rₜ_m = Rₜ[m]
        Rₜ_adj_m = Rₜ_adj[m]
        
        last_time_step = last_time_steps[m]

        # Imputation of `num_impute` days
        cases_pred_m[1] = zero(eltype(cases_pred_m))
        cases_pred_m[2:num_impute] = cumsum(fill(y[m], num_impute - 1)) .+ cases_pred_m[1]

        daily_cases_pred_m[1:num_impute] .= y[m]
        
        xs = covariates[m][1:last_time_step, :] # extract covariates for the wanted time-steps and country `m`
        Rₜ_m .= μ[m] * exp.(xs * (-α) + (- lockdown[m]) * xs[:, lockdown_index])

        # adjusts for portion of pop that are susceptible
        Rₜ_adj_m[1:num_impute] .= (max.(pop_m .- cases_pred_m[1:num_impute], zero(cases_pred_m[1])) ./ pop_m) .* Rₜ_m[1:num_impute]

        for t = (num_impute + 1):last_time_step
            cases_pred_m[t] = cases_pred_m[t - 1] + daily_cases_pred_m[t - 1]

            Rₜ_adj_m[t] = (max(pop_m - cases_pred_m[t], zero(cases_pred_m[t])) / pop_m) * Rₜ_m[t] # adjusts for portion of pop that are susceptible
            daily_cases_pred_m[t] = Rₜ_adj_m[t] * sum([daily_cases_pred_m[τ] * serial_intervals[t - τ] for τ = 1:(t - 1)])
        end

        expected_daily_deaths_m[1] = 1e-15 * daily_cases_pred_m[1]
        for t = 2:last_time_step
            expected_daily_deaths_m[t] = sum([daily_cases_pred_m[τ] * πₘ[t - τ] * ifr_noise[m] for τ = 1:(t - 1)])
        end
    end

    # Observe
    <<jl:observe>>

    return (
        daily_cases_pred = daily_cases_pred,
        expected_daily_deaths = expected_daily_deaths,
        Rₜ = Rₜ,
        Rₜ_adjusted = Rₜ_adj
    )
end;
#+end_src

Two things worth noting is the use of this =TV= variable to instantiate some internal variables and the use of =@threads=. 

As you can see in the arguments for the model, =TV= refers to a /type/ and will be recognized as such by the =@model= macro when transforming the model code. This is used to ensure /type-stability/ of the model.

#+HTML: <details><summary>More detailed explanation of <code>TV</code></summary>

A default execution of the model will then use =TV= as =Vector{Float64}=, thus making statements like =TV(undef, n)= result in a =Vector{Float64}= with =undef= (uninitialized) values and length =n=. But in the case where we want to take use a sampler using automatic differentiation (AD), e.g. =HMC=, this =TV= will be replaced with the AD-type corresponding to a =Vector=, e.g. =TrackedVector= in the case of [[https://github.com/FluxML/Tracker.jl][=Tracker.jl=]].

#+HTML: </details>

The use of =@threads= means that inside each execution of the =Model=, this loop will be performed in parallel, where the number of threads are specified by the enviroment variable =JULIA_NUM_THREADS=. This is thanks to the really nice multithreading functionality [[https://julialang.org/blog/2019/07/multithreading/][introduced in Julia 1.3]] (and so this also requires Julia 1.3 or higher to run the code). Note that the inside the loop is independent of each other and each =m= will be seen by only one thread, hence it's threadsafe.

This model is basically identitical to the one defined in [[https://github.com/ImperialCollegeLondon/covid19model/blob/6ee3010a58a57cc14a16545ae897ca668b7c9096/stan-models/base.stan][stan-models/base.stan (#6ee3010)]] with the exception of two points:
- in this model we use =TruncatedNormal= for normally distributed variables which are positively constrained
- we've added the use of =max(pop_m - cases_pred_m[t], 0)= in computing the /adjusted/ Rₜ, =Rₜ_adj=, to ensure that in the case where the entire populations has died there, the adjusted Rₜ is set to 0, i.e. if everyone in the country passed away then there is no spread (this does not affect "correctness" of inference) [fn:3]

*** Multithreaded observe
We can also make the =observe= statements parallel, but because the =~= is not (yet) threadsafe we unfortunately have to touch some of the internals of =Turing.jl=. But for observations it's very straight-forward. Instead of observing by the following piece of code

#+name: jl:observe
#+begin_src julia :eval no :tangle no
for m = 1:num_countries
    # Extract the estimated expected daily deaths for country `m`
    expected_daily_deaths_m = expected_daily_deaths[m]
    # Extract time-steps for which we have observations
    ts = epidemic_start[m]:num_obs_countries[m]
    # Observe!
    deaths[m][ts] ~ arraydist(NegativeBinomial2.(expected_daily_deaths_m[ts], ϕ))
end
#+end_src

we can use the following

#+name: jl:observe-threaded
#+begin_src julia :eval no :tangle no
# Doing observations in parallel provides a small speedup
logps = TV(undef, num_countries)
@threads for m = 1:num_countries
    # Extract the estimated expected daily deaths for country `m`
    expected_daily_deaths_m = expected_daily_deaths[m]
    # Extract time-steps for which we have observations
    ts = epidemic_start[m]:num_obs_countries[m]
    # Observe!
    logps[m] = logpdf(arraydist(NegativeBinomial2.(expected_daily_deaths_m[ts], ϕ)), deaths[m][ts])
end
_varinfo.logp[] += sum(logps)
#+end_src

#+HTML: <details><summary>Explanation of what we just did</summary>

It might be worth explaining a bit about what's going on here. First we should explain what the deal is with =_varinfo.logp[]=. =_varinfo= is basically the object used internally in Turing to track the sampled variables and the logpdf /for a particular evaluation/ of the model. The logpdf is stored in the =_varinfo.logp= field, which is a reference/pointer for performance reasons. =[]= deferences this, allowing us to mutate the value that it's pointing to. With that we can explain what happens to =~= inside the =@macro=. Using the old observe-snippet as an example, the =@model= macro replaces =~= with

#+begin_src julia :eval no :tangle no
_varinfo.logp[] += logpdf(arraydist(NegativeBinomial2.(expected_daily_deaths_m[ts], ϕ)), deaths[m][ts])
#+end_src

But we're iterating through =m=, so this would not be thread-safe since you might be two threads attempting to mutate =varinfo.logp[]= simultaneously.[fn:2] Therefore, since no threads sees the same =m=, delaying the accumulation to after having computed all the log-pdf in parallel leaves us with equivalent code that is threadsafe.

You can read more about the =@macro= and its internals [[https://turing.ml/dev/docs/for-developers/compiler#model-macro-and-modelgen][here]].

#+HTML: </details>

*** Final model
This results in the following model definition

#+begin_src julia :noweb yes
@model model_v2(
    num_impute,        # [Int] num. of days for which to impute infections
    num_total_days,    # [Int] days of observed data + num. of days to forecast
    cases,             # [AbstractVector{<:AbstractVector{<:Int}}] reported cases
    deaths,            # [AbstractVector{<:AbstractVector{<:Int}}] reported deaths; rows indexed by i > N contain -1 and should be ignored
    π,                 # [AbstractVector{<:AbstractVector{<:Real}}] h * s
    covariates,        # [Vector{<:AbstractMatrix}]
    epidemic_start,    # [AbstractVector{<:Int}]
    population,        # [AbstractVector{<:Real}]
    serial_intervals,  # [AbstractVector{<:Real}] fixed pre-calculated serial interval (SI) using empirical data from Neil
    lockdown_index,    # [Int] the index for the `lockdown` covariate in `covariates`
    predict=false,     # [Bool] if `false`, will only compute what's needed to `observe` but not more
    ::Type{TV} = Vector{Float64}
) where {TV} = begin
    # `covariates` should be of length `num_countries` and each entry correspond to a matrix of size `(num_total_days, num_covariates)`
    num_covariates = size(covariates[1], 2)
    num_countries = length(cases)
    num_obs_countries = Vector(undef, num_countries)
    for m = 1:num_countries
        num_obs_countries[m] = length(cases[m])
    end

    # Latent variables
    <<jl:priors>>

    # If we don't want to predict the future, we only need to compute up-to time-step `num_obs_countries[m]`
    last_time_steps = predict ? fill(num_total_days, num_countries) : num_obs_countries

    # Initialization of some quantities
    expected_daily_cases = TV[TV(undef, last_time_steps[m]) for m in 1:num_countries]
    cases_pred = TV[TV(undef, last_time_steps[m]) for m in 1:num_countries]
    expected_daily_deaths = TV[TV(undef, last_time_steps[m]) for m in 1:num_countries]
    Rₜ = TV[TV(undef, last_time_steps[m]) for m in 1:num_countries]
    Rₜ_adj = TV[TV(undef, last_time_steps[m]) for m in 1:num_countries]

    # Loops over countries and perform independent computations for each country
    # since this model does not include any notion of migration across borders.
    # => might has well wrap it in a `@threads` to perform the computation in parallel.
    @threads for m = 1:num_countries
        # country-specific parameters
        πₘ = π[m]
        pop_m = population[m]
        expected_daily_cases_m = expected_daily_cases[m]
        cases_pred_m = cases_pred[m]
        expected_daily_deaths_m = expected_daily_deaths[m]
        Rₜ_m = Rₜ[m]
        Rₜ_adj_m = Rₜ_adj[m]
        
        last_time_step = last_time_steps[m]

        # Imputation of `num_impute` days
        cases_pred_m[1] = zero(eltype(cases_pred_m))
        cases_pred_m[2:num_impute] = cumsum(fill(y[m], num_impute - 1)) .+ cases_pred_m[1]

        expected_daily_cases_m[1:num_impute] .= y[m]
        
        xs = covariates[m][1:last_time_step, :] # extract covariates for the wanted time-steps and country `m`
        Rₜ_m .= μ[m] * exp.(xs * (-α) + (- lockdown[m]) * xs[:, lockdown_index])

        # adjusts for portion of pop that are susceptible
        Rₜ_adj_m[1:num_impute] .= (max.(pop_m .- cases_pred_m[1:num_impute], zero(cases_pred_m[1])) ./ pop_m) .* Rₜ_m[1:num_impute]

        for t = (num_impute + 1):last_time_step
            cases_pred_m[t] = cases_pred_m[t - 1] + expected_daily_cases_m[t - 1]

            Rₜ_adj_m[t] = (max(pop_m - cases_pred_m[t], zero(cases_pred_m[t])) / pop_m) * Rₜ_m[t] # adjusts for portion of pop that are susceptible
            expected_daily_cases_m[t] = Rₜ_adj_m[t] * sum([expected_daily_cases_m[τ] * serial_intervals[t - τ] for τ = 1:(t - 1)])
        end

        expected_daily_deaths_m[1] = 1e-15 * expected_daily_cases_m[1]
        for t = 2:last_time_step
            expected_daily_deaths_m[t] = sum([expected_daily_cases_m[τ] * πₘ[t - τ] * ifr_noise[m] for τ = 1:(t - 1)])
        end
    end

    # Observe
    <<jl:observe-threaded>>

    # These are the "generated quantities" that we're interested in analysing
    return (
        expected_daily_cases = expected_daily_cases,
        expected_daily_deaths = expected_daily_deaths,
        Rₜ = Rₜ,
        Rₜ_adjusted = Rₜ_adj
    )
end;
#+end_src

We define an alias =model_def= so that if we want to try out a different model, there's only one point in the notebook which we need to change.

#+begin_src julia
model_def = model_v2;
#+end_src


The input data have up to 30-40 days of unobserved /future/ data which we might want to predict on. During sampling we don't need to predict for future dates since we do not have any observations for these cases yet. Therefore we have an argument =predict::Bool= in the model definition which allows us to specify whether or not to generate future quantities.

#+begin_src julia
# Model instantance used to for inference
m_no_pred = model_def(
    data.num_impute,
    data.num_total_days,
    data.cases,
    data.deaths,
    data.π,
    data.covariates,
    data.epidemic_start,
    data.population,
    data.serial_intervals,
    lockdown_index,
    false # <= DON'T predict
);
#+end_src

#+begin_src julia
# Model instance used for prediction
m = model_def(
    data.num_impute,
    data.num_total_days,
    data.cases,
    data.deaths,
    data.π,
    data.covariates,
    data.epidemic_start,
    data.population,
    data.serial_intervals,
    lockdown_index,
    true # <= predict
);
#+end_src

Just to make sure everything is working, we can "evaluate" the model to obtain a sample from the prior:

#+begin_src julia
res = m();
res.expected_daily_cases[uk_index]
#+end_src

* Visualization utilities
For visualisation we of course use [[https://github.com/JuliaPlots/Plots.jl][Plots.jl]], and in this case we're going to use the =pyplot= backend which uses Python's matplotlib under the hood.

#+begin_src julia
using Plots, StatsPlots
pyplot()
#+end_src

#+HTML: <details><summary>Method definition for plotting the predictive distribution</summary>

#+begin_src julia
# Ehh, this can be made nicer...
function country_prediction_plot(country_idx, predictions_country::AbstractMatrix, e_deaths_country::AbstractMatrix, Rₜ_country::AbstractMatrix; normalize_pop::Bool = false)
    pop = data.population[country_idx]
    num_total_days = length(data.deaths[country_idx])
    num_observed_days = data.num_obs_countries[country_idx]

    country_name = countries[country_idx]
    start_date = first(country_to_dates[country_name])
    dates = cumsum(fill(Day(1), data.num_total_days)) + (start_date - Day(1))
    date_strings = Dates.format.(dates, "Y-mm-dd")

    # A tiny bit of preprocessing of the data
    preproc(x) = normalize_pop ? x ./ pop : x

    daily_deaths = data.deaths[country_idx][1:num_observed_days]
    daily_cases = data.cases[country_idx][1:num_observed_days]
    
    p1 = plot(; xaxis = false, legend = :topleft)
    bar!(preproc(daily_deaths), label="$(country_name)")
    title!("Observed daily deaths")
    vline!([data.epidemic_start[country_idx]], label="epidemic start", linewidth=2)
    vline!([data.num_obs_countries[country_idx]], label="end of observations", linewidth=2)
    xlims!(0, num_total_days)

    p2 = plot(; legend = :topleft, xaxis=false)
    plot_confidence_timeseries!(p2, preproc(e_deaths_country); label = "$(country_name)")
    title!("Expected daily deaths (pred)")
    bar!(preproc(daily_deaths), label="$(country_name) (observed)", alpha=0.5)

    p3 = plot(; legend = :bottomleft, xaxis=false)
    plot_confidence_timeseries!(p3, Rₜ_country; no_label = true)
    for (c_idx, c_time) in enumerate(findfirst.(==(1), eachcol(data.covariates[country_idx])))
        if c_time !== nothing
            # c_name = names(covariates)[2:end][c_idx]
            c_name = names_covariates[c_idx]
            if (c_name != "any")
                # Don't add the "any intervention" stuff
                vline!([c_time - 1], label=c_name)
            end
        end
    end
    title!("Rₜ")
    qs = [quantile(v, [0.025, 0.975]) for v in eachrow(Rₜ_country)]
    lq, hq = (eachrow(hcat(qs...))..., )
    ylims!(0, maximum(hq) + 0.1)

    # p3 = bar(replace(data.cases[country_idx], missing => -1.), label="$(country_name)")
    # title!("Daily cases")

    p4 = plot(; legend = :topleft, xaxis=false)
    plot_confidence_timeseries!(p4, preproc(predictions_country); label = "$(country_name)")
    title!("Expected daily cases (pred)")
    bar!(preproc(daily_cases), label="$(country_name) (observed)", alpha=0.5)

    vals = preproc(cumsum(e_deaths_country; dims = 1))
    p5 = plot(; legend = :topleft, xaxis=false)
    plot_confidence_timeseries!(p5, vals; label = "$(country_name)")
    plot!(preproc(cumsum(daily_deaths)), label="observed", color=:red)
    title!("Expected deaths (pred)")

    vals = preproc(cumsum(predictions_country; dims = 1))
    p6 = plot(; legend = :topleft)
    plot_confidence_timeseries!(p6, vals; label = "$(country_name)")
    plot!(preproc(daily_cases), label="observed", color=:red)
    title!("Expected cases (pred)")

    p = plot(p1, p3, p2, p4, p5, p6, layout=(6, 1), size=(900, 1200), sharex=true)
    xticks!(1:3:num_total_days, date_strings[1:3:end], xrotation=45)

    return p
end
                                        
function country_prediction_plot(country_idx, cases, e_deaths, Rₜ; kwargs...)
    n = length(cases)
    e_deaths_country = hcat([e_deaths[t][country_idx] for t = 1:n]...)
    Rₜ_country = hcat([Rₜ[t][country_idx] for t = 1:n]...)
    predictions_country = hcat([cases[t][country_idx] for t = 1:n]...)

    return country_prediction_plot(country_idx, predictions_country, e_deaths_country, Rₜ_country; kwargs...)
end
#+end_src

#+HTML: </details>

* Prior
Before we do any inference, it can be useful to inspect the /prior/ distribution, in particular if you are working with a hierarchical model where the dependencies in the prior might lead to some unexpected behavior. In Turing.jl you can sample a chain from the prior using =sample=, much in the same way as you would sample from the posterior.

#+begin_src julia
chain_prior = sample(m, Turing.Inference.PriorSampler(), 1_000);
#+end_src

#+begin_src julia
plot(chain_prior[[:ϕ, :τ, :κ]]; α = .5, linewidth=1.5)
#+end_src

For the same reasons it can be very useful to inspect the /predictive prior/.

#+begin_src julia
# Compute the "generated quantities" for the PRIOR
generated_prior = vectup2tupvec(generated_quantities(m, chain_prior));
daily_cases_prior, daily_deaths_prior, Rₜ_prior, Rₜ_adj_prior = generated_prior;
#+end_src

#+begin_src julia
country_prediction_plot(uk_index, daily_cases_prior, daily_deaths_prior, Rₜ_prior)
#+end_src

And with the Rₜ /adjusted for remaining population/:

#+begin_src julia
country_prediction_plot(uk_index, daily_cases_prior, daily_deaths_prior, Rₜ_adj_prior)
#+end_src

At this point it might be useful to remind ourselves of the total population of UK is:

#+begin_src julia
data.population[uk_index]
#+end_src

As we can see from the figures, the prior allows scenarios such as
- /all/ of the UK being infected
- effects of interventions, e.g. =lockdown=, having a /negative/ effect on =Rₜ= (in the sense that it can actually /increase/ the spread)
But at the same time, it's clear that a very sudden increase jump from 0% to 100% of the population being infected is almost impossible under the prior. All in all, it seems the model prior seems reasonable: it allows for extreme situations without putting too much probabilty "mass" on those, while still encoding some structure in the model.

* Posterior inference
#+begin_src julia
parameters = (
    warmup = 1000,
    steps = 3000
);
#+end_src

** Inference
*** Run
To perform inference for the model we would simply run the code below:
#+begin_src julia :eval no :exports code
chains_posterior = sample(m_no_pred, NUTS(parameters.warmup, 0.95, 10), parameters.steps + parameters.warmup)
#+end_src

/But/ unfortunately it takes quite a while to run. Performing inference using =NUTS= with =1000= steps for adaptation/warmup and =3000= sample steps takes ~4hrs on a 6-core computer with =JULIA_NUM_THREADS = 6=. And so we're instead just going to load in the chains needed.

In contrast, =Stan= only takes roughly 1hr /on a single thread/ using the base model from the repository. On a single thread =Turing.jl= is ~10X slower for this model. This is quite signficant.

This generally means that if you have a clear model in mind (or you're already very familiar with =Stan=), you probably want to use =Stan= for these kind of models. On the other hand, if you're in the process of heavily tweaking your model and need to be able to do =m = model(data...); m()= to check if it works, or you want more flexibility in what you can do with your model, =Turing.jl= might be the best option.

And this is an additional reason why we wanted to perform this replication study: we want =Turing.jl= to be /useful/ and the way to check this is by applying =Turing.jl= to real-world problem rather than just benchmarks (though those are important too).

And regarding the performance difference, it really comes down to the difference in implementation of automatic differentation (AD). =Turing.jl= allows you to choose from the goto AD packages in Julia, e.g. in our runs we used [[https://github.com/JuliaDiff/ForwardDiff.jl][ForwardDiff.jl]], while =Stan= as a very, very fast AD implementation written exclusively for =Stan=. This difference becomes very clear in models such as this one where we have a lot of for-loops and recursive relationships (because this means that we can't easily vectorize). For-loops in Julia are generally blazingly fast, but with AD there's a bit of overhead. But that also means that in =Turing.jl= you have the ability to choose between different approaches to AD, i.e. forward-mode or reverse-mode, each with their different tradeoffs, and thus will benefit from potentially interesting future work, e.g. source-to-source AD using [[https://github.com/FluxML/Zygote.jl][Zygote.jl]].[fn:1]

And one interesting tidbit is that you can very easily use =pystan= within =PyCall.jl= to sample from a =Stan= model, and then convert the results into a [[https://github.com/TuringLang/MCMCChains.jl][MCMCChains.jl]]. This has some nice implications:
- we can use all the convenient posterior analysis tools available in MCMCChains.jl to analyze chains from =Stan=
- we can use the =generated_quantities= method in this notebook to execute the =Turing.jl= =Model= on the samples obtain using =Stan=
This was quite useful for us to be able validate the results from =Turing.jl= against those from =Stan=, and made it very easy to check that indeed =Turing.jl= and =Stan= produce the same results. You can find examples of this in the [[https://github.com/TuringLang/Covid19][notebooks in our repository]].

*** Load
Unfortunately the resulting chains, each with 3000 steps, take up a fair bit of space and are thus too large to include in the Github repository. As a temporary hack around this, you can find download the chains from [[https://drive.google.com/open?id=16PomGVnjPI1Q4KLdA9gRloVolfRmrhPP][this link]]. Then you simply navigate to the project-directory and unpack.

With that, we can load the chains from disk:

#+begin_src julia
filenames = [
    projectdir("out", s) for s in readdir(projectdir("out"))
    if occursin(savename(parameters), s) && occursin("seed", s)
]
length(filenames)
#+end_src

#+begin_src julia
chains_posterior_vec = [read(fname, Chains) for fname in filenames]; # read the different chains
chains_posterior = chainscat(chains_posterior_vec...); # concatenate them
chains_posterior = chains_posterior[1:3:end] # <= thin so we're left with 1000 samples

# rename some variables to make the chain compatible with new model where we've changed a variable name from μ₀ → μ
chains_posterior = set_names(chains_posterior, Dict{String, String}(["μ₀[$i]" => "μ[$i]" for i = 1:length(names(chains_posterior[:μ₀]))]))
#+end_src

#+begin_src julia
plot(chains_posterior[[:κ, :ϕ, :τ]]; α = .5, linewidth=1.5)
#+end_src

#+begin_src julia
# Compute generated quantities for the chains pooled together
pooled_chains = MCMCChains.pool_chain(chains_posterior)
generated_posterior = vectup2tupvec(generated_quantities(m, pooled_chains));

daily_cases_posterior, daily_deaths_posterior, Rₜ_posterior, Rₜ_adj_posterior = generated_posterior;
#+end_src

The predictive posterior:

#+begin_src julia
country_prediction_plot(uk_index, daily_cases_posterior, daily_deaths_posterior, Rₜ_posterior)
#+end_src

and with the adjusted Rₜ:

#+begin_src julia
country_prediction_plot(uk_index, daily_cases_posterior, daily_deaths_posterior, Rₜ_adj_posterior)
#+end_src

** All countries: prior vs. posterior predictive
For the sake of completeness, here are the predictive priors and posteriors for all the 14 countries in a side-by-side comparison.

#+HTML: <div class="two-by-two">

#+begin_src julia :exports none
country_idx = 0
#+end_src

#+name: plot-next-country-prior
#+begin_src julia :exports results
country_idx += 1
country_prediction_plot(country_idx, daily_cases_prior, daily_deaths_prior, Rₜ_adj_prior)
#+end_src

#+name: plot-next-country-posterior
#+begin_src julia :exports results
country_prediction_plot(country_idx, daily_cases_posterior, daily_deaths_posterior, Rₜ_adj_posterior)
#+end_src

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/beb81e270177e21ebb766d0fa8231378b8ee7573.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/183e66747789ca4a202669949f7b4688ca64c41d.png]]

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/c7aa41858955654fbac9b290dbdcb82bf2a60d59.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/d992d6010b9d1fa061d7d51d6723c8af3b2fc53f.png]]

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/5bc13a9fce6fefe520d95edf34352b875c1492ab.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/45e58ffb07a4f2edd3eebb81978c73065d792caf.png]]

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/79be0bf8847567128a9578fa33c6107c51cb8965.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/2ae30d246b8cf7e9399aa352d7e5343501391eed.png]]

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/637e0349137170b12c106b88354232b1e1da5898.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/958e68558a4f494df5343169e28fb30566396744.png]]

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/0e34e13f858d8f688bf290f38fcefba1fd8a1117.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/66166539eb0d32aac0b8f2eaac86edf2a44a020b.png]]

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/236156f940b233ddd9674852659bc93472be1dc0.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/2c5a30ccf2bf39781a59e0859076cbcaf7846f1c.png]]

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/b46c0e9f57323545293727d264b58dbcdee8ee0e.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/5d780264daf52a4c4c64845bcd86e3621d7d439b.png]]

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/f12d4ee99df4c2c600efd5c9e1a64525e8f440f4.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/1f4b3cdc9975e9cc6bc15aea7388e6371df0f3a6.png]]

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/6013543f00429880b5e0800517c2b69b7856ff6b.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/fb52437b06c827b2814488929d9501a67302781f.png]]

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/eddef8491616a7918a9e6fe80c3aa3a87eed4350.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/d1dbffad75a10e292b02f8b6e6d6dd12acb0134c.png]]

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/567221d0e2729594f34f38829eac1197b44ee15d.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/35c58bdbce9ec344ac61a827682f622df402880b.png]]

#+CALL: plot-next-country-prior()

#+RESULTS:
[[file:./.ob-jupyter/5833cc048ac9e4e6848f0afb5837bf6f6f77f5c1.png]]

#+CALL: plot-next-country-posterior()

#+RESULTS:
[[file:./.ob-jupyter/327b03d1bdf15f5117fd5e80e6b59d38fc036e8c.png]]

#+HTML: </div>

** What if we didn't do any/certain interventions?
One interesting thing one can do after obtaining estimates for the effect of each of the interventions is to run the model but now /without/ all or a subset of the interventions performed. Thus allowing us to get a sense of what the outcome would have been without those interventions, and also whether or not the interventions have the wanted effect.

=data.covariates[m]= is a binary matrix for each =m= (country index), with =data.covariate[m][:, k]= then being a binary vector representing the time-series for the k-th covariate: =0= means the intervention has not yet been implemented, =1= means that the intervention has been implemented. As an example, if schools and universites were closed after the 45th day for country =m=, then =data.covariate[m][1:45, k]= are all zeros and =data.covariate[m][45:end, k]= are all ones.

#+begin_src julia
# Get the index of schools and univerities closing
schools_universities_closed_index = findfirst(==("schools_universities"), names_covariates)
# Time-series for UK
data.covariates[uk_index][:, schools_universities_closed_index]
#+end_src

Notice that this is under the assumptions that not only are schools and universities closed /at some point/, but rather that they also stay closed in the future (at the least the future that we are considering).

Therefore we can for example simulate "what happens if we never closed schools and universities?" by instead setting this entire vector to =0= and re-run the model on the infererred parameters, similar to what we did before to compute the "generated quantities", e.g. Rₜ.

First a convenience function for zeroing out subsets of the interventions:

#+begin_src julia
"""
    zero_covariates(xs::AbstractMatrix{<:Real}; remove=[], keep=[])

Allows you to zero out covariates if the name of the covariate is in `remove` or NOT zero out those in `keep`.
Note that only `remove` xor `keep` can be non-empty.

Useful when instantiating counter-factual models, as it allows one to remove/keep a subset of the covariates.
"""
zero_covariates(xs::AbstractMatrix{<:Real}; kwargs...) = zero_covariates(xs, names_covariates; kwargs...)
function zero_covariates(xs::AbstractMatrix{<:Real}, names_covariates; remove=[], keep=[])
    @assert (isempty(remove) || isempty(keep)) "only `remove` or `keep` can be non-empty"
    
    if isempty(keep)
        return hcat([
            (names_covariates[i] ∈ remove ? zeros(eltype(c), length(c)) : c) 
            for (i, c) in enumerate(eachcol(xs))
        ]...)
    else
        return hcat([
        (names_covariates[i] ∈ keep ? c : zeros(eltype(c), length(c))) 
        for (i, c) in enumerate(eachcol(xs))
        ]...)
    end
end
#+end_src

Now we can consider simulation under the posterior with /no/ intervention, and we're going to visualize the respective /portions/ of the population by rescaling by total population:

#+begin_src julia
# What happens if we don't do anything?
m_counterfactual = model_def(
    data.num_impute,
    data.num_total_days,
    data.cases,
    data.deaths,
    data.π,
    [zeros(size(c)) for c in data.covariates], # <= remove ALL covariates
    data.epidemic_start,
    data.population,
    data.serial_intervals,
    lockdown_index,
    true # <= use full model
);

# Compute the "generated quantities" for the "counter-factual" model
generated_counterfactual = vectup2tupvec(generated_quantities(m_counterfactual, pooled_chains));
daily_cases_counterfactual, daily_deaths_counterfactual, Rₜ_counterfactual, Rₜ_adj_counterfactual = generated_counterfactual;
country_prediction_plot(5, daily_cases_counterfactual, daily_deaths_counterfactual, Rₜ_adj_counterfactual; normalize_pop = true)
#+end_src


We can also consider the cases where we only do /some/ of the interventions, e.g. we never do a full lockdown (=lockdown=) or close schools and universities (=schools_universities=):

#+begin_src julia
# What happens if we never close schools nor do a lockdown?
m_counterfactual = model_def(
    data.num_impute,
    data.num_total_days,
    data.cases,
    data.deaths,
    data.π,
    [zero_covariates(c; remove = ["lockdown", "schools_universities"]) for c in data.covariates], # <= remove covariates
    data.epidemic_start,
    data.population,
    data.serial_intervals,
    lockdown_index,
    true # <= use full model
);

# Compute the "generated quantities" for the "counter-factual" model
generated_counterfactual = vectup2tupvec(generated_quantities(m_counterfactual, pooled_chains));
daily_cases_counterfactual, daily_deaths_counterfactual, Rₜ_counterfactual, Rₜ_adj_counterfactual = generated_counterfactual;
country_prediction_plot(uk_index, daily_cases_counterfactual, daily_deaths_counterfactual, Rₜ_adj_counterfactual; normalize_pop = true)
#+end_src

As mentioned, this assumes that we will stay in lockdown and schools and universities will be closed in the future. We can also consider, say, removing the lockdown, i.e. opening up, at some future point in time:

#+begin_src julia
lift_lockdown_time = 75

new_covariates = [copy(c) for c in data.covariates] # <= going to do inplace manipulations so we copy
for covariates_m ∈ new_covariates
    covariates_m[lift_lockdown_time:end, lockdown_index] .= 0
end

# What happens if we never close schools nor do a lockdown?
m_counterfactual = model_def(
    data.num_impute,
    data.num_total_days,
    data.cases,
    data.deaths,
    data.π,
    new_covariates,
    data.epidemic_start,
    data.population,
    data.serial_intervals,
    lockdown_index,
    true # <= use full model
);

# Compute the "generated quantities" for the "counter-factual" model
generated_counterfactual = vectup2tupvec(generated_quantities(m_counterfactual, pooled_chains));
daily_cases_counterfactual, daily_deaths_counterfactual, Rₜ_counterfactual, Rₜ_adj_counterfactual = generated_counterfactual;
country_prediction_plot(uk_index, daily_cases_counterfactual, daily_deaths_counterfactual, Rₜ_adj_counterfactual; normalize_pop = true)
#+end_src

* Conclusion
Well, there isn't one. As stated before, drawing conclusions is not the purpose of this document. With that being said, we /are/ working on exploring this and other models further, e.g. relaxing certain assumptions, model validation & comparison, but this will hopefully be available in a more technical and formal report sometime in the near future after proper validation and analysis. But since time is of the essence in these situations, we thought it was important to make the above and related code available to the public immediately. At the very least it should be comforting to know that two different PPLs both produce the same inference results when the model might be used to inform policy decisions on a national level.

If you have any questions or comments, feel free to reach out either on the [[https://github.com/TuringLang/Covid19][Github repo]] or to any of us personally.


* Footnotes

[fn:3] It's important to point out that in Stan these samples will be correctly rejected because it leads to =-Inf= joint probability later in model. Hence inference will be correct even if =max= is not used.

[fn:2] You /could/ use something like [[https://docs.julialang.org/en/v1/base/multi-threading/#Base.Threads.Atomic][Atomic in Julia]], but it comes at a unnecessary performance overhead in this case.

[fn:1] Recently one of our team-members joined as a maintainer of [[https://github.com/JuliaDiff/ReverseDiff.jl][ReverseDiff.jl]] to make sure that =Turing.jl= also has fast and reliable reverse-mode differentiation. ReverseDiff.jl is already compatible with =Turing.jl=, but we hope that this will help make if much, much faster.
